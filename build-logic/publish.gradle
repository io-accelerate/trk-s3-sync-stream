
// Refs:
// https://stackoverflow.com/a/68505768
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = 'Java client for TDL system'
                description = 'A Java client that allows users to get up to speed with the TDL system.'
                url = 'https://github.com/io-accelerate/trk-s3-sync-stream'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Iulian (Julian) Ghionoiu'
                        email = 'iulian.ghionoiu@gmail.com'
                        organization = 'Accelerate Systems'
                        organizationUrl = 'https://github.com/io-accelerate'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:io-accelerate/trk-s3-sync-stream.git'
                    developerConnection = 'scm:git:git@github.com:io-accelerate/trk-s3-sync-stream.git'
                    url = 'git@github.com:io-accelerate/trk-s3-sync-stream.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty('mavenCentralUsername')
                password = project.findProperty('mavenCentralPassword')
            }
        }
    }
}

// Signs using gpg-agent
// Key name configured under "~/.gradle/gradle.properties"
//
// Refs:
// https://docs.gradle.org/7.4.1/userguide/signing_plugin.html#sec:using_gpg_agent
signing {
    useGpgCmd()
    sign publishing.publications.release
}
