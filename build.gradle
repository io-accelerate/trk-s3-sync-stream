plugins {
    // Ref here: https://docs.gradle.org/7.4.1/userguide/publishing_setup.html
    id 'java-library'
    id 'application'
    id 'signing'
    id 'maven-publish'
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'ro.ghionoiu'
version = project.version.toString()

repositories {
    mavenCentral()
    mavenLocal()
}

// ~~~~~ Dependencies

dependencies {
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.127'
    implementation 'com.beust:jcommander:1.64'

    implementation 'commons-logging:commons-logging:1.1.1'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.1.3'
    implementation 'org.slf4j:log4j-over-slf4j:1.7.25'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.25'

    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
    testImplementation('org.junit.platform:junit-platform-suite:1.8.1')
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.11.0'

    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'net.trajano.commons:commons-testing:1.0.1'
}

// ~~~~~ Test

test {
    useJUnitPlatform()
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty "http.proxyHost", System.getProperty('http.proxyHost')
    systemProperty "http.proxyPort", System.getProperty('http.proxyPort')
    systemProperty "https.proxyHost", System.getProperty('https.proxyHost')
    systemProperty "https.proxyPort", System.getProperty('https.proxyPort')
}

// ~~~~~ Packaging

mainClassName = "tdl.s3.SyncFileApp"
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

// Suppress Javadoc warnings
javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

//~~~~~ Distribution

// Refs:
// https://stackoverflow.com/a/68505768
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = 'S3 Sync Stream'
                description = 'Library that continuously syncs the contents of a folder to an S3 bucket. Optimised for streaming file formats (video, logs).'
                url = 'https://github.com/julianghionoiu/s3-sync-stream'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Iulian (Julian) Ghionoiu'
                        email = 'iulian.ghionoiu@gmail.com'
                        organization = 'Iulian Ghionoiu'
                        organizationUrl = 'https://github.com/julianghionoiu'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:julianghionoiu/s3-sync-stream.git'
                    developerConnection = 'scm:git:git@github.com:julianghionoiu/s3-sync-stream.git'
                    url = 'git@github.com:julianghionoiu/s3-sync-stream.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty('mavenCentralUsername')
                password = project.findProperty('mavenCentralPassword')
            }
        }
    }
}

// Signs using gpg-agent
// Key name configured under "~/.gradle/gradle.properties"
//
// Refs:
// https://docs.gradle.org/7.4.1/userguide/signing_plugin.html#sec:using_gpg_agent
signing {
    useGpgCmd()
    sign publishing.publications.release
}
